

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "2.2.0"

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 28
        versionCode 1
        versionName "1.1"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions{
        abortOnError false
        // This seems to be firing due to okio referencing java.nio.File
        // which is harmless for us.
        warning 'InvalidPackage',
                // don't need parcel creator for the sub-class of MessageSnapshot.
                'ParcelCreator'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:support-v4:28.0.0'
}


def siteUrl = 'https://github.com/fanOfDemo/FloatMenuSample'            // 项目的主页
def gitUrl = 'https://github.com/fanOfDemo/FloatMenuSample.git'   // Git仓库的url
group = "com.yw.game.floatmenu" // Maven Group ID for the artifact，一般填你唯一的包名
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'FloatMenu'  //项目描述
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The BSD 3-Clause License'
                        url 'http://www.linfo.org/bsdlicense.html'
                    }
                }
                developers {
                    developer {
                        id 'yw'       //填写开发者基本信息
                        name 'fanofdemo'
                        email '18720625976@163.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}



//tasks.findByPath(":FloatMenu:androidJavadocs").enabled = false

task javadoc(type: Javadoc) {
    options.encoding = "utf-8"
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "FloatMenu"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["The BSD 3-Clause License"]
        publish = true
    }
}


