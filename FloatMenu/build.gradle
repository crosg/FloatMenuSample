apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

version = "2.4.0"
group = "com.github.fanofdemo"

android {
    namespace 'com.yw.game.floatmenu'
    compileSdk 36

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 36
        versionCode 1
        versionName "1.1"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lint {
        abortOnError false
        warning 'InvalidPackage', 'ParcelCreator'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'org.jetbrains:annotations:24.0.1'
}

// GitHub Packages 发布配置
afterEvaluate {
    publishing {
        publications {
            gpr(MavenPublication) {
                artifactId = 'FloatMenu'

                artifact("$buildDir/outputs/aar/FloatMenu-release.aar") {
                    builtBy tasks.named('assembleRelease')
                }

                artifact(sourcesJar)

                groupId = 'com.github.fanofdemo'
                version = '2.4.0'

                pom {
                    name = 'FloatMenu'
                    description = 'A lightweight and easy-to-use Android floating menu library'
                    url = 'https://github.com/fanOfDemo/FloatMenuSample'

                    licenses {
                        license {
                            name = 'The BSD 3-Clause License'
                            url = 'https://opensource.org/licenses/BSD-3-Clause'
                        }
                    }

                    developers {
                        developer {
                            id = 'coldbrando'
                            name = 'ColdBrando'
                            email = 'coderbrando@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/fanOfDemo/FloatMenuSample.git'
                        developerConnection = 'scm:git:ssh://github.com/fanOfDemo/FloatMenuSample.git'
                        url = 'https://github.com/fanOfDemo/FloatMenuSample/tree/master'
                    }

                    withXml {
                        def dependenciesNode = asNode().appendNode('dependencies')
                        configurations.implementation.allDependencies.each {
                            if (it.name != 'unspecified' && it.group != null) {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)
                            }
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/fanOfDemo/FloatMenuSample")
                credentials {
                    username = System.getenv("GITHUB_USER") ?: project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.key") ?: System.getenv("TOKEN")
                }
            }
        }
    }
}

tasks.register('sourcesJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}
